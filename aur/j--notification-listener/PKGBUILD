# Maintainer: i at joram dot io

pkgname=j--notification-listener-git
pkgver=r2.17d3447
pkgrel=2
pkgdesc='A notification listener that forwards to notify-send'
arch=('x86_64')
url="https://git.sr.ht/~joram/notification-listener"
license=('GPL3')
depends=('libnotify')
makedepends=('git' 'elixir')
source=(
    "git+https://git.sr.ht/~joram/notification-listener"
    "notification-listener.service"
)
sha512sums=(
    'SKIP'
    'f0f87fffa0d4c2e56db7cb7922b0df14075b09e9aca1cec5fe041d41cf9af5927817b8a9cbf4de692b71d4f4991030b92caba57c8d8953b21a1e8619caa4bc55'
)

pkgver() {
  cd "$srcdir/notification-listener"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/notification-listener"

  export MIX_ENV=prod
  export MIX_HOME="$srcdir/mix-cache"

  mix local.hex --force
  mix deps.get
  mix compile
}

package() {
  cd "$srcdir/notification-listener"

  export MIX_ENV=prod

  install -dm0755 "$pkgdir"/usr/lib/$pkgname
  mix release --path "$pkgdir"/usr/lib/$pkgname

  install -dm0755 "$pkgdir"/usr/bin
  echo -e "#!/bin/sh\nexec /usr/lib/$pkgname/bin/notification_listener \$@" > "$pkgdir"/usr/bin/notification-listener
  chmod +x "$pkgdir"/usr/bin/notification-listener

  install -Dm644 "$srcdir/notification-listener.service" "$pkgdir/usr/lib/systemd/user/notification-listener.service"
}
